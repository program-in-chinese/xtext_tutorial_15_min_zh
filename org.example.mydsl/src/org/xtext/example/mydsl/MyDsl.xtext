grammar org.example.domainmodel.Domainmodel with org.eclipse.xtext.common.Terminals

generate domainmodel "http://www.example.org/domainmodel/Domainmodel"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Domainmodel:
	(elements+=AbstractElement)*;

PackageDeclaration:
	'包' name=QualifiedName '{'
	(elements+=AbstractElement)*
	'}';

AbstractElement:
	PackageDeclaration | Type | Import;

QualifiedName:
	IDENTIFIER ('.' IDENTIFIER)*;

Import:
	'导入' importedNamespace=QualifiedNameWithWildcard;

QualifiedNameWithWildcard:
	QualifiedName '.*'?;

Type:
	DataType | Entity;

DataType:
	'数据类型' name=IDENTIFIER;

Entity:
	'类' name=IDENTIFIER ('扩展' superType=[Entity|QualifiedName])? '{'
	(features+=Feature)*
	'}';

Feature:
	(many?='复数')? name=IDENTIFIER ':' type=[Type|QualifiedName];
	
terminal IDENTIFIER: '^'?('\u4E00'..'\u9FA5'|'\uF900'..'\uFA2D'|'a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9'|'\u4E00'..'\u9FA5'|'\uF900'..'\uFA2D')*;